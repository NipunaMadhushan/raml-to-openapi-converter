import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'java'
    id "net.researchgate.release" version "2.8.0"
}

group = 'org.ballerina.ramltoopenapi'
version = project.version

def moduleVersion = project.version.replace("-SNAPSHOT", "")

// Configure repositories for all subprojects
allprojects {
    group = project.group
    version = project.version

    apply plugin: 'maven-publish'

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url = 'https://maven.pkg.github.com/ballerina-platform/*'
            credentials {
                username System.getenv("packageUser")
                password System.getenv("packagePAT")
            }
        }
    }
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

def executeBalCommand(String command, String dir, env = "") {
    try {
        exec {
            workingDir dir
            environment environment: env
            if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                commandLine 'cmd', '/c', "bal.bat ${command} && exit %%ERRORLEVEL%%"
            } else {
                commandLine 'sh', '-c', "bal ${command}"
            }
        }
    } catch (Exception e) {
        println("bal command failed. " + e.message)
        throw e
    }
}

release {
    failOnPublishNeeded = false
    failOnSnapshotDependencies = true

    buildTasks = ['build']
    versionPropertyFile = 'gradle.properties'
    tagTemplate = 'v$version'

    git {
        requireBranch = "release-${moduleVersion}"
        pushToRemote = 'origin'
    }
}

test {
    useJUnitPlatform()
}

//release.dependsOn ":cli-raml-to-openapi:ballerinaPublish"
